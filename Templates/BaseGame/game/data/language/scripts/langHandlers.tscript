//------------------------------------------------------------------------------
// Language selection control and command handler.
//------------------------------------------------------------------------------

/// Adds the language popup on the main menu.
function language::installControls(%this)
{
   if (isObject(MainMenuGui))
   {
      %header = new GuiTextCtrl() {
         textID = "lang_header";
         maxLength = "255";
         position = "16 732";
         extent = "150 20";
         horizSizing = "right";
         vertSizing = "top";
         profile = "MenuSubHeaderRight";
         visible = "1";
         active = "1";
         tooltipProfile = "GuiToolTipProfile";
         hovertime = "1000";
         isContainer = "0";
         canSave = "0";
         canSaveDynamicFields = "0";
      };

      %popup = new GuiPopUpMenuCtrl() {
         maxPopupHeight = "200";
         sbUsesNAColor = "0";
         reverseTextList = "0";
         bitmapBounds = "16 16";
         maxLength = "255";
         position = "174 733";
         extent = "160 18";
         minExtent = "8 2";
         horizSizing = "right";
         vertSizing = "top";
         profile = "GuiPopUpMenuProfile";
         visible = "1";
         active = "1";
         command = "language.onLangSelected($ThisControl);";
         tooltipProfile = "GuiToolTipProfile";
         hovertime = "1000";
         isContainer = "0";
         class = "LanguagePopup";
         canSave = "0";
         canSaveDynamicFields = "0";
      };

      MainMenuGui.add(%header);
      MainMenuGui.add(%popup);
   }
}

function language::onLangSelected(%this, %popup)
{
   %selected = %popup.getSelected();
   if (%this.activeLang == %selected)
      return; // No change

   %langCode = %this.langTable.getLangCode(%selected);
   $pref::Lang::active = %langCode;
   %this.activeLang = %selected;
   %this.langTable.setCurrentLanguage(%this.activeLang);

   // Schedule a screen refresh so the change takes effect on the current gui.
   $GameCanvas.schedule(32, "setContent", $GameCanvas.getContent());
   $GameCanvas.setContent(StartupGui);
}

function LanguagePopup::onWake(%this)
{
   if (!isObject(language) || !isObject(language.langTable))
      return;

   %this.clear();

   // Add all installed languages.
   %numLangs = language.langTable.getNumLang();
   %selected = 0;
   for(%i = 0; %i < %numLangs; %i++)
   {
      %langName = language.langTable.getLangName(%i);
      %this.add(%langName, %i);
      if ($pref::Lang::active $= language.langTable.getLangCode(%i))
         %selected = %i;
   }
   %this.setSelected(%selected, false);
}

language.installControls();